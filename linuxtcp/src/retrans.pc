#ifndef RETRANS_PC
#define RETRANS_PC

module RetransmitM.TCB :> ParentTCB
has SegmentList, .Segment, .Socket, .Output, TopTCB
{
  
  field rexmit-timeout :> short;
  field rexmit-shift :> short;
  
  field _out_of_order_queue :> SegmentList @ 328;
  
  constructor(s :> *Socket) ::=
    ParentTCB(s),
    rexmit-shift = 0,
    calculate-retransmit-timeout;
  
  
  // REASSEMBLY QUEUE

  reassembly-queue :> *Segment ::=
    _out_of_order_queue.head;
  
  reassembly-queue-tail :> *Segment ::=
    _out_of_order_queue.tail;
  
  append-to-reassembly-queue(seg :> *Segment) ::=
    assertion(!reassembly-queue || seg->seqno > reassembly-queue-tail->seqno),
    _out_of_order_queue.enqueue-tail(seg);
  
  print ::=
    inline super.print,
    (reassembly-queue ==> print-reassembly-queue);

  print-reassembly-queue ::=
    { PDEBUG("     reass Q:"); },
    _out_of_order_queue.print-list,
    { PDEBUG("\n"); };

  
  // RETRANSMISSION BACKOFF
  
  max-shift :> short ::= 12;
  
  step-backoff :> bool ::=
    rexmit-shift++,
    (rexmit-shift > max-shift ==> rexmit-shift = max-shift);
  
  backoff-value :> short ::=
    (rexmit-shift <= 5 ==> 1 << rexmit-shift) ||| 64;
  
  
  // RETRANSMIT TIMEOUT VALUE
  
  max-retransmit-timeout :> short ::= 128;
  
  calculate-retransmit-timeout ::=
    let to = estimated-rtt * backoff-value in
      to max= min-rtt,
      to min= max-retransmit-timeout,
      rexmit-timeout = to,
#ifdef PRINT
      print-retransmission-values
#endif
    end;
  
  
  // RETRANSMIT TIMER
  
  start-retransmit-timer ::=
#ifdef PRINT
    { PDEBUG("starting retransmit timer -> %d\n", rexmit_timeout); }
#endif
    set-retransmit-timer(rexmit-timeout);
  
  retransmit-alarm ::=
    super.retransmit-alarm,
    do-alarm;
  
  do-alarm {
    
    do-alarm ::=
      (step-backoff ==> too-much-backoff)
      || ok;
    
    too-much-backoff ::=
      { assert(0 && "dropping connection"); };
    
    ok ::=
      calculate-retransmit-timeout,
      start-retransmit-timer,
      (connection-losing ==> notify-losing),
      start-retransmission,
      Output((*TopTCB)&self).run;
    
    connection-losing :> bool ::=
      rexmit-shift > 3;
    
    notify-losing ::=
      /* SOCK in_losing v2p842 */
      transparently-revoke-rtt-estimate;
    
  }
  
  
  // MARKS
  
  update-rtt-estimate(rtt :> int) ::=
    inline super.update-rtt-estimate(rtt),
    rexmit-shift = 0,
    calculate-retransmit-timeout;
  
  total-ack-hook ::=
    super.total-ack-hook,
    cancel-retransmit;
  
  send-hook(seqlen :> uint) ::=
    inline super.send-hook(seqlen),
    (!is-retransmit-set && !recently-acked ==> start-retransmit-timer
     /* XXX persist */);
  
  
  // PRINTING
  
  print-retransmission-values ::=
    let xx = backoff-value in
      { PDEBUG("set rexmit to %d (%d/%d)\n",
	       rexmit_timeout, rexmit_shift, xx); }
    end;

} inline (backoff-value)
  hide ("TCB._*", rexmit-shift, rexmit-timeout, backoff-value, max-shift, 
	step-backoff, set-retransmit-timer);

module RetransmitM.ParentTCB ::= CUR_TCB;
#undef CUR_TCB
#define CUR_TCB RetransmitM.TCB
#endif
